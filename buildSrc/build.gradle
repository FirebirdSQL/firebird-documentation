plugins {
    id 'groovy'
    id 'java-gradle-plugin'
}

gradlePlugin {
    plugins {
        documentationPlugin {
            id = 'org.firebirdsql.documentation'
            implementationClass = 'org.firebirdsql.documentation.DocumentationPlugin'
        }
    }
}

repositories {
    mavenCentral()
    // To resolve jai-code and jai-core
    maven {
        url 'https://repository.jboss.org/nexus/content/repositories/thirdparty-releases/'
    }
    // 'repository' for getting docbook xsd
    ivy {
        url 'https://docbook.org/xml'
        patternLayout {
            artifact '[revision]/[module]-[revision].[ext]'
        }
        content {
            includeGroup 'docbook'
        }
        metadataSources {
            artifact()
        }
    }
    // 'repository' for getting additional dependencies not available from Maven
    ivy {
        url 'https://www.firebirdsql.org/doclibs/'
        patternLayout {
            artifact '[module].[ext]'
        }
        content {
            includeGroup 'org.firebirdsql.doclibs'
        }
        metadataSources {
            artifact()
        }
    }
}

dependencies {
    compileOnly gradleApi()
    compileOnly localGroovy()
    implementation 'xerces:xercesImpl:2.12.0'
    implementation 'xml-resolver:xml-resolver:1.2'
    implementation 'saxon:saxon:6.5.3'
    implementation 'net.sf.xslthl:xslthl:2.1.3'

    implementation 'org.apache.xmlgraphics:fop:2.4'

    implementation 'net.sf.docbook:docbook-xsl:1.79.1:resources@zip'

    // dependencies not on Maven
    // https://docbook.org/xml/4.5/docbook-xml-4.5.zip
    implementation 'docbook:docbook-xml:4.5:resources@zip'
    
//    implementation 'org.firebirdsql.doclibs:foxon:1@jar'
//    implementation 'org.firebirdsql.doclibs:jai_codec:1.1.2_01@jar'
//    implementation 'org.firebirdsql.doclibs:jai_core:1.1.2_01@jar'
}
