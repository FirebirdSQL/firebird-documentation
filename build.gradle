import org.asciidoctor.gradle.jvm.AsciidoctorJExtension
import org.asciidoctor.gradle.jvm.AsciidoctorTask
import org.asciidoctor.gradle.jvm.pdf.AsciidoctorPdfTask
import org.asciidoctor.gradle.jvm.pdf.AsciidoctorPdfThemesExtension
import org.firebirdsql.documentation.docbook.DocbookFo
import org.firebirdsql.documentation.docbook.DocbookHtml
import org.firebirdsql.documentation.fop.FoTask
import org.firebirdsql.documentation.generic.XsltTask

plugins {
    id 'org.firebirdsql.documentation'
    id 'org.asciidoctor.jvm.base' version '3.1.0'
    id 'org.asciidoctor.jvm.pdf' version '3.1.0' apply false
}

repositories {
    jcenter()
}

asciidoctorj {
    modules {
        pdf {
            version '1.5.3'
        }
    }
}

extensions.create(AsciidoctorPdfThemesExtension.NAME, AsciidoctorPdfThemesExtension, project)
extensions.getByType(AsciidoctorJExtension).modules.pdf.use()

pdfThemes {
    local 'firebird', {
        themeDir = file('src/theme/firebird-pdf')
        themeName = 'firebird'
    }
}

// TODO Define custom tasks to build subset of documents

def asciidocHtml = tasks.register('asciidocHtml', AsciidoctorTask) {
    group 'Documentation'
    description 'Generate HTML documentation from asciidoc'
    sourceDir file('src/docs/asciidoc')
    outputDir layout.buildDirectory.dir("docs/asciidoc/html")
    languages 'en'
    baseDirFollowsSourceFile()
    def stylesDir = file('src/theme/firebird-html')
    attributes 'stylesdir': file('src/theme/firebird-html').absolutePath, 'stylesheet': 'firebird.css',
            'docinfo': 'shared', 'docinfodir': file("$stylesDir/docinfo").absolutePath
    inputs.dir(stylesDir)
    doLast {
        // copy images referenced in docinfo-header
        languages.each { lang ->
        copy {
                from "$stylesDir/docinfo/images"
                into "$outputDir/$lang/images"
            }
        }
    }
    // TODO Need more general solution for images
}

def asciidocPdf = tasks.register('asciidocPdf', AsciidoctorPdfTask) {
    group 'Documentation'
    description 'Generate PDF documentation from asciidoc'
    sourceDir file('src/docs/asciidoc')
    outputDir layout.buildDirectory.dir("docs/asciidoc/pdf")
    languages 'en'
    baseDirFollowsSourceFile()
    attributes 'revnumber': null, 'source-highlighter': 'rouge', 'media': 'prepress', 'toc': 'macro', 'compress': '',
            'toclevels': 3
    theme 'firebird'
}

docConfig {
    configRootDir.set(file('config'))
    docRoot.set(file('src/docs'))
    styleDir.set(file('src/docs/xsl'))
    outputRoot.set(layout.buildDirectory.dir('docs'))
    defaultBaseName.set('firebirddocs')
}

task docbookHtml(type: DocbookHtml, group: 'documentation') {
    description 'Generate chunked HTML documentation'
    stylesheetBaseName.set('html')
    outputTypeName.set('html')
    imageExcludes.addAll(
            '*.txt',
            'firebird_logo_400x400.png')
    extraFilesToOutput.set(files('src/docs/firebirddocs.css'))
}

task docbookMonohtml(type: DocbookHtml, group: 'documentation') {
    description 'Generate single file HTML documentation'
    stylesheetBaseName.set('monohtml')
    outputTypeName.set('monohtml')
    imageExcludes.addAll(
            '*.txt',
            'firebird_logo_400x400.png')
    extraFilesToOutput.set(files('src/docs/firebirddocs.css'))
}

def docbookFoTask = tasks.register('docbookFo', DocbookFo)
def docbookPdfTask = tasks.register('docbookPdf', FoTask)
docbookPdfTask.configure { fo2pdf ->
    fo2pdf.group 'documentation'
    fo2pdf.description 'Generate PDF documentation from FO file'
    fo2pdf.inputFoFile.set(docbookFoTask.get().mainOutputFile)
    fo2pdf.dependsOn docbookFoTask
}
docbookFoTask.configure { docbookFo ->
    docbookFo.group 'documentation'
    docbookFo.description 'Generate FO documentation'
    docbookFo.stylesheetBaseName.set('fo')
    docbookFo.outputTypeName.set('fo')
    docbookFo.imageExcludes.addAll(
            '*.txt',
            'titleblackgill.gif',
            'firebirdlogo.png',
            'toc-*.png',
            'top-*.png',
            'prev-*.png',
            'next-*.png')
    docbookFo.configureWith(docbookPdfTask.get())
}

task "tt-fo"(type: XsltTask, group: "tools") {
    description 'Generates fo/titlepage.templates.xsl from fo/titlepage.templates.xml'
    inputFile.set(docConfig.styleDir.file('fo/titlepage.templates.xml'))
    styleSheetUri.set('template/titlepage.xsl')
    stylesheetBase.set('http://docbook.sourceforge.net/release/xsl/current/')
    outputFile.set(docConfig.styleDir.file('fo/titlepage.templates.xsl'))
}

task "tt-html"(type: XsltTask, group: "tools") {
    description 'Generates html/titlepage.templates.xsl from html/titlepage.templates.xml'
    inputFile.set(docConfig.styleDir.file('html/titlepage.templates.xml'))
    styleSheetUri.set('template/titlepage.xsl')
    stylesheetBase.set('http://docbook.sourceforge.net/release/xsl/current/')
    outputFile.set(docConfig.styleDir.file('html/titlepage.templates.xsl'))
}
